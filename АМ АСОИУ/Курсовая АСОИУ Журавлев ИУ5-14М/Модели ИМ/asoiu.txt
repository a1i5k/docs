INITIAL       X$STATION_N,25 	; Кол-во рабочих станций
INITIAL       X$STATION_TD,0	; Среднее время дораб. запр. на ПК
INITIAL       X$STATION_TF,100	; Средн. время формир. Запр. на ПК
INITIAL       X$CANAL_T,1	; Среднее время перед. через канал
INITIAL       X$SERVER_T,5	; Средн. вр. обраб. запр. на проц.
INITIAL       X$DISK_N,1	; Количество дисков
INITIAL       X$DISK_T,10	; Средн. вр. обраб. запр. на диске
INITIAL       X$PROP,0.01	; Вер-ть обращ. запр. к ЦП после д
INITIAL	    X$SHAPE,1	; Форма кривой гамма-распределения

WORKSTATION_D     STORAGE	25	; Кол-во РС
WORKSTATION_F     STORAGE	25	; Также кол-во раб. станций

SERVER  STORAGE      1	; Кол-во процессоров
DISK_N  FUNCTION     RN1,D2 
0.5,1/1,1		;Кол-во д.(последн. цифра)

           	GENERATE     ,,,X$STATION_N 
             
WOSF   	QUEUE          	QSYSTEM
	QUEUE		QFORM
        ENTER        	WORKSTATION_F,1
        ADVANCE       	(Gamma(1,0,X$STATION_TF,X$SHAPE))
        LEAVE         	WORKSTATION_F,1
	DEPART		QFORM
        ASSIGN          	3,SVR

CAN     	QUEUE           	QCANAL
        SEIZE        	CANAL
        ADVANCE        	(Gamma(1,X$CANAL_T,1,X$SHAPE))
        RELEASE         	CANAL
	DEPART       	QCANAL
        TRANSFER       	,P3 

SVR     	QUEUE		QSERVER
	ENTER     	SERVER,1
        ADVANCE        	(Gamma(1,X$SERVER_T,1,X$SHAPE))
        LEAVE          	SERVER,1
	DEPART		QSERVER
        ASSIGN   	5,FN$DISK_N  
        QUEUE            	P5 
        SEIZE            	P5 
        ADVANCE         	(Gamma(1,X$DISK_T,1,X$SHAPE))
        RELEASE         	P5
        DEPART     	P5
        TRANSFER      	X$PROP,PER,SVR

PER    	ASSIGN          	3,WOSD
        TRANSFER      	,CAN 

WOSD   	ENTER         	WORKSTATION_D,1
        ADVANCE        	(Gamma(1,X$STATION_TD,1,X$SHAPE))
        LEAVE    	WORKSTATION_D,1
	DEPART		QSYSTEM
        TRANSFER        	,WOSF  
        GENERATE   	100000
        TERMINATE  	1 
        START 1